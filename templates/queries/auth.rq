{{ prefixes }}

construct {

    {{this}} ?p ?o .

    ?item a ?item_type;
      dc11:title ?item_title;
      dc11:creator {{this}} .

    {{this}}
      dbpowl:abstract ?abstract;
      dbpowl:thumbnail ?thumbnail;
      dbpowl:influencedBy ?influence .

    ?influence
      foaf:name ?influence_name;
      dbpowl:thumbnail ?influence_thumbnail .

    #{% if services.raa %}
    {{this}} owl:sameAs?ksam_this .
    ?ksam_this ksamsok:itemType <http://kulturarvsdata.se/resurser/EntityType#person> ;
      ksamsok:itemLabel ?ksam_name .

    ?ksam_hubobj a ?ksam_hubobj_type;
      ksamsok:name ?ksam_hubobj_name;
      ksamsok:architect ?ksam_this .

    ?ksam_thing ksamsok:itemType ?ksam_thing_type;
      ksamsok:url ?ksam_thing_url;
      ksamsok:context ?ksam_hubobj .
    #{% endif %}

    #{% if services.ra %}
    {{this}} owl:sameAs ?nad_this .
    ?nad_this ?nad_p ?nad_o .
    #{% endif %}

} where {

    # LIBRIS (default) endpoint
    {

      {{this}} ?p ?o .

    } union {

      select ?item ?item_type ?item_title {
        ?item a ?item_type;
          dc11:title ?item_title;
          dc11:creator {{this}} .
        filter(langMatches(lang(?item_title), 'sv'))
      } limit 50

    #{% if services.dbp %}
    # DBPedia endpoint
    } union {

      {{this}} owl:sameAs ?canonical .

      service <{{ services.dbp }}> {
        ?canonical dbpowl:abstract ?abstract .
        #FILTER(langMatches(lang(?abstract), "sv"))
        FILTER(langMatches(lang(?abstract), "en"))
        optional {
          ?canonical dbpowl:thumbnail ?thumbnail .
        }
        optional {
          ?canonical dbpowl:influencedBy ?canonical_influence .
          ?canonical_influence foaf:name ?influence_name .
          optional { ?canonical_influence dbpowl:thumbnail ?influence_thumbnail }
        }
      }
      # workaround for shaky bound implementation..
      BIND(COALESCE(?canonical_influence, ?_) as ?bound_canonical_influence)
      optional {
        FILTER(BOUND(?bound_canonical_influence))
        ?influence owl:sameAs ?canonical_influence .
      }
    #{% endif %}

    #{% if services.raa %}
    ## RAA endpoint
    } union {

      service <{{ services.raa }}> {

        ?ksam_this ksamsok:itemType <http://kulturarvsdata.se/resurser/EntityType#person> ;
          owl:sameAs {{this}};
          ksamsok:itemLabel ?ksam_name .

        ?ksam_hubobj
          ksamsok:name ?ksam_hubobj_name;
          ksamsok:architect ?ksam_this; # ksamsok:owner ...
          a ?ksam_hubobj_type .

        #{ select ?ksam_thing ?ksam_thing_type ?ksam_thing_url  {
        #  ?ksam_thing ksamsok:context ?ksam_hubobj .
        #  #FILTER(?ksam_repr_rel in (ksamsok:context , ksamsok:image))
        #  ?ksam_thing
        #    ksamsok:itemType ?ksam_thing_type; # <http://kulturarvsdata.se/resurser/EntityType#building>;
        #    ksamsok:url ?ksam_thing_url .
        #} limit 1 }


        #?ksam_this ksamsok:fullName ?fullName .
      }
    #{% endif %}

    #{% if services.ra %}
    # RA endpoint
    } union {

      service <{{ services.ra }}> {
        ?nad_this owl:sameAs ?canonical .
        #?nad_this foaf:name ?fullName .
        ?nad_this ?nad_p ?nad_o .
      }
    #{% endif %}

    }

}

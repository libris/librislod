construct {

    $this ?p ?o .

    ?item a ?item_type;
      dces:title ?item_title;
      dces:creator $this .

    $this
      dbpowl:abstract ?abstract;
      dbpowl:thumbnail ?thumbnail;
      dbpowl:influencedBy ?influence .

    ?influence
      foaf:name ?influence_name;
      dbpowl:thumbnail ?influence_thumbnail .

    $this owl:sameAs ?ksam_this .
    ?ksam_this ?ksam_p ?ksam_o .

    $this owl:sameAs ?nad_this .
    ?nad_this ?nad_p ?nad_o .

} where {

    $this owl:sameAs ?canonical .

    # LIBRIS (default) endpoint
    {

      $this ?p ?o .

    } union {

      select ?item ?item_type ?item_title {
        ?item a ?item_type;
          dces:title ?item_title;
          dces:creator $this .
        filter(langMatches(lang(?item_title), 'sv'))
      } limit 50

    # DBPedia endpoint
    } union {

      service <http://dbpedia.org/sparql> {
        ?canonical dbpowl:abstract ?abstract .
        #FILTER(langMatches(lang(?abstract), "sv"))
        FILTER(langMatches(lang(?abstract), "en"))
        optional {
          ?canonical dbpowl:thumbnail ?thumbnail .
        }
        optional {
          ?canonical dbpowl:influencedBy ?canonical_influence .
          ?canonical_influence foaf:name ?influence_name .
          optional { ?canonical_influence dbpowl:thumbnail ?influence_thumbnail }
        }
      }
      # workaround for shaky bound implementation..
      BIND(COALESCE(?canonical_influence, ?_) as ?bound_canonical_influence)
      optional {
        FILTER(BOUND(?bound_canonical_influence))
        ?influence owl:sameAs ?canonical_influence .
      }

    ## RAA endpoint
    #} union {

    #  service <http://192.121.221.30/sparql/raa> {
    #    {
    #      ?ksam_this owl:sameAs $this .
    #    } union {
    #      ?ksam_this owl:sameAs ?canonical .
    #    }
    #    ?ksam_this ?ksam_p ?ksam_o .
    #    #?ksam_this ksamsok:fullName ?fullName .
    #  }

    ## RA endpoint
    #} union {

    #  service <http://192.121.221.30/sparql/ra> {
    #    ?nad_this owl:sameAs ?canonical .
    #    #?nad_this foaf:name ?fullName .
    #    ?nad_this ?nad_p ?nad_o .
    #  }

    }

}

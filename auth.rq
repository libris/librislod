construct {

    $this ?p ?o .

    ?item a ?item_type;
      dces:title ?item_title;
      dces:creator $this .

    $this
      dbpowl:abstract ?abstract;
      dbpowl:thumbnail ?thumbnail .

    $this owl:sameAs ?ksam_this .
    ?ksam_this ?ksam_p ?ksam_o .

    $this owl:sameAs ?nad_this .
    ?nad_this ?nad_p ?nad_o .

} where {

    $this owl:sameAs ?canonical .

    # LIBRIS (default) endpoint
    {

      $this ?p ?o .

    } union {

      select ?item ?item_type ?item_title {
        ?item a ?item_type;
          dces:title ?item_title;
          dces:creator $this .
        filter(langMatches(lang(?item_title), 'sv'))
      } limit 50

    ## DBPedia endpoint
    } union {

      service <http://dbpedia.org/sparql> {
        ?canonical dbpowl:abstract ?abstract .
        #FILTER(langMatches(lang(?abstract), "sv"))
        FILTER(langMatches(lang(?abstract), "en"))
        optional {
          ?canonical dbpowl:thumbnail ?thumbnail .
        }
      }

    # RAA endpoint
    } union {

      service <http://192.121.221.30/sparql/raa> {
        {
          ?ksam_this owl:sameAs ?canonical .
        } union {
          ?ksam_this owl:sameAs $this .
        }
        ?ksam_this ?ksam_p ?ksam_o .
      }

    # RA endpoint
    } union {

      service <http://192.121.221.30/sparql/ra> {
        ?nad_this owl:sameAs ?canonical;
          ?nad_p ?nad_o .
      }

    }

}
